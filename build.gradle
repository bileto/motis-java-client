plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.openapi.generator' version '7.17.0'
    id 'com.github.ben-manes.versions' version'0.53.0'
}

group = 'dev.bileto'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'jakarta.annotation:jakarta.annotation-api:3.0.0'
    implementation 'jakarta.validation:jakarta.validation-api:3.1.1'

    implementation 'org.openapitools:jackson-databind-nullable:0.2.7'

    implementation 'org.springframework:spring-webflux:6.2.12'
    implementation 'io.projectreactor.netty:reactor-netty:1.2.11'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.20.0'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.20'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.0'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.20.0'
    implementation 'org.apache.commons:commons-lang3:3.19.0'
    
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.junit.jupiter:junit-jupiter:6.0.0'
    testImplementation 'org.mockito:mockito-core:5.20.0'
    testImplementation 'org.springframework:spring-test:6.2.12'
    testImplementation 'io.projectreactor:reactor-test:3.7.12'
    testImplementation 'com.squareup.okhttp3:mockwebserver3:5.2.1'

    testRuntimeOnly 'org.springframework:spring-context:6.2.12'
}

test {
    useJUnitPlatform()
}

openApiGenerate {
    generatorName = 'java'
    inputSpec = "${rootDir}/ext/motis/openapi.yaml".replace("\\","/").toString()
    outputDir = "${rootDir}".toString()
    packageName = 'dev.bileto.motis.client'
    apiPackage = 'dev.bileto.motis.client.api'
    modelPackage = 'dev.bileto.motis.client.model'
    invokerPackage = 'dev.bileto.motis.client'

    configOptions = [
        annotationLibrary: 'none',
        library: 'webclient',
        dateLibrary: 'java8',
        documentationProvider: 'none',
        java8: 'true',
        generateSupportingFiles: 'false',
        hideGenerationTimestamp: 'true',
        parcelableModel: 'false',
        serializationLibrary: 'jackson',
        useJakartaEe: 'true',
        useSpringBoot3: 'true',
        webclientBlockingOperations: 'true'
    ]
}

tasks.named("openApiGenerate") {
    doNotTrackState("Generating to root dir")
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
/*
            groupId = project.group
            artifactId = project.name
            version = project.version
*/
            pom {
                name = 'MOTIS Java Client'
                description = 'Java client for MOTIS API'
                url = 'https://github.com/bileto/motis-java-client'
/*                
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
*/
                developers {
                    developer {
                        id = 'bileto'
                        name = 'Bileto'
                        email = 'info@bileto.com'
                    }
                }
/*
                scm {
                    connection = 'scm:git:git://github.com/bileto/motis-java-client.git'
                    developerConnection = 'scm:git:ssh://github.com:bileto/motis-java-client.git'
                    url = 'https://github.com/bileto/motis-java-client/tree/master'
                }
*/
            }
        }
    }
    
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/bileto/motis-java-client"
            credentials {
                username = System.getenv("GITHUB_USERNAME") ?: project.findProperty("gpr.user")
                password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.key")
            }
        }
    }
}
